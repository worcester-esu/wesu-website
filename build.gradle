plugins {
	id 'org.jbake.site' version '5.0.0'
	id 'org.hidetake.ssh' version '2.10.1'
}

group = 'uk.co.wesu'

remotes {
	webServer {
		host = project.findProperty("uk.co.wesu.deploy.host")
		user = project.findProperty("uk.co.wesu.deploy.user")
		password = project.findProperty("uk.co.wesu.deploy.password")
		knownHosts = addHostKey(file("${System.properties['user.home']}/.ssh/known_hosts"))
	}
}

task zip(type: Zip) {
	from "${buildDir}/jbake/"
	archiveName 'web.zip'
	destinationDir(file("${buildDir}/deploy/"))
}

task deploy {
	doLast {
		ssh.run {
			session(remotes.webServer) {
				def deployDir = project.findProperty("uk.co.wesu.deploy.location")
				def tempDeployDir = project.findProperty("uk.co.wesu.deploy.temp")
				put from: "${buildDir}/deploy/", into: tempDeployDir
				execute "unzip ${tempDeployDir}/deploy/web.zip -d ${tempDeployDir}/extracted/"
				execute "rm --preserve-root -Rf ${deployDir}/*", ignoreError: true
				execute "mv ${tempDeployDir}/extracted/* ${deployDir}/"
				execute "rm --preserve-root -Rf ${tempDeployDir}/*", ignoreError: true
			}
		}
	}
}

zip.dependsOn bake
assemble.dependsOn zip
deploy.dependsOn build
